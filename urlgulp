#!/usr/bin/perl
$version = "1.0";

###################################################
# Default Settings

$interface = "eth0";
$interval = 5;
# 0: quiet mode (domains only)
# 1: default mode (coloring, grouping)
# 2: raw mode
$print_urls = 1;

# $url_trunc_first = "tail";
# $url_trunc_first = "path";
$url_trunc_first = "balanced";

$group_really_long_urls = 0;
$really_long_thres = 100;

$debug = 0;
$tcp_only = 0;
$wait_quiet = 0;
$key_mode = 1;  # default now
$show_agent = 0;
$show_legend = 0;

###################################################

sub usage
{
    print "Usage: urlgulp [options] [-i interface]\n\n";
    print "display mode:\n";
    print "  -k            wait for key press to display (default).\n";
    print "  -f            display every interval seconds.\n";
    print "  -w            display after interval seconds of silence.\n";
    print "\n";
    print "other options:\n";
    print "  -d <interval> set display interval for -f and -w (default: 5s).\n";
    print "  -u            show user agent used for each request.\n";
    print "  -l            show legend.\n";
    print "  -q            quiet mode. Don't log urls, only domains.\n";
    print "  -r            raw url mode. just dump it all, no url coloring, grouping or truncating.\n";
    print "  -v            debug mode (use twice for more).\n";
    print "  --tcp-only    track tcp connections only (no urls)\n";
    print "                will show which IP addresses are actually being contacted,\n";
    print "                useful to check whether a remote proxy is really being used or not.\n";
    print "  --version     show version.\n";
    exit(1);
}

# ansi color codes
%color = ( "wb" => "\033[40;37;1m",  # white bold
	   "rb" => "\033[40;31;1m",  # red bold
	   "yb" => "\033[40;33;1m",  # yellow bold
	   "pb" => "\033[40;35;1m",  # purple bold
	   "bb" => "\033[40;34;1m",  # blue bold
	   "cb" => "\033[40;36;1m",  # cyan bold
	   "e" => "\033[0m");

# terminal width
my $columns = `tput cols`;
# amount of space displayed in front of each url.
my $url_spc = "                    ";

for (; @ARGV != 0; shift(@ARGV))
{
    if ($ARGV[0] eq "-q")
    {
	$print_urls = 0;
	next;
    }
    if ($ARGV[0] eq "-r")
    {
	$print_urls = 2;
	next;
    }
    if ($ARGV[0] eq "--tcp-only")
    {
	$tcp_only = 1;
	next;
    }
    if ($ARGV[0] eq "--version")
    {
	print "urlgulp version $version.\n";
	exit 0;
    }
    if ($ARGV[0] eq "-i")
    {
	shift(@ARGV);
	$interface = $ARGV[0];
	next;
    }
    if ($ARGV[0] eq "-d")
    {
	shift(@ARGV);
	$interval = $ARGV[0];
	next;
    }
    if ($ARGV[0] eq "-w")
    {
	$wait_quiet = 1;
	$key_mode = 0;
	next;
    }
    if ($ARGV[0] eq "-f")
    {
	$key_mode = 0;
	next;
    }
    if ($ARGV[0] eq "-k")
    {
	$key_mode = 1;
	next;
    }
    if ($ARGV[0] eq "-v")
    {
	$debug++;
	next;
    }
    if ($ARGV[0] eq "-u")
    {
	$show_agent = 1;
	next;
    }
    if ($ARGV[0] eq "-l")
    {
	$show_legend = 1;
	next;
    }

    usage();
}

sub key_mode_prompt
{
    print "buffering requests, press ENTER to display "
}

# Make stdout line buffered.
$|=1;

# intro message
print "urlgulp: listening on $interface";
if ($key_mode)
{ print ".\n" }
elsif ($wait_quiet)
{ print ", display after ${interval}s of silence.\n\n"; }
else
{ print ", display every ${interval}s.\n\n"; }

key_mode_prompt() if ($key_mode);

##############################################################
# read input from terminal

# maybe one day i'll use select instead of this beautiful hack ...
if ($key_mode)
{ 
    $key_cmd = "perl -e '\$|=1; open(IN, \"< /dev/tty\"); while (my \$s=<IN>){ print \"key press\\n\"; }' &";
}

##############################################################
# http requests sniffing

my $urlsnarf_cmd = "urlsnarf -n -i $interface";
#my $urlsnarf_cmd = "cat testurltrunc.log";

##############################################################
# dns and new tcp connection sniffing (other than http)
# TODO: log udp traffic
# TODO: log ipv6 traffic

my $pcap_filter = "udp port 53 or (not tcp port 80 and tcp[tcpflags] = tcp-syn)";
if ($tcp_only)
{  $pcap_filter = "udp port 53 or                      tcp[tcpflags] = tcp-syn "; }
my $tcpdump_cmd ="tcpdump -s 500 -l -n -i $interface '$pcap_filter'"; 

my %dns_req;
my %rev_dns;

my %dns_alias;
my %filtered_cdns = (
    "akamai.net" => 1,
    "akadns.net" => 1, 
    "akamaiedge.net" => 1,
    "bluehatnetwork.com" => 1,
    "cachefly.net" => 1,
    "cdngc.net" => 1,
    "cubecdn.net" => 1,
    "cedexis.net" => 1,
    "cloudfront.net" => 1,
    "cloudflare.com" => 1,
    "cotcdn.net" => 1,
    "dnspro.org" => 1,
    "edgekey.net" => 1,
    "edgecastcdn.net" => 1,
    "edgesuite.net" => 1,
    "fastly.net" => 1,
    "footprint.net" => 1,
    "hwcdn.net" => 1,
    "instacontent.net" => 1,
    "internapcdn.net" => 1,
    "llnwd.net" => 1,
    "lxdns.com" => 1,
    "mirror-image.net" => 1,
    "netdna-cdn.com" => 1,
    "panthercdn.com" => 1,
    "simplecdn.net" => 1,
    "swiftcdn1.com" => 1,
    "upcache.com" => 1,
    "voxcdn.net" => 1,
    "yahoodns.net" => 1);
    
    
###############################################################################
# display summary

my %req_by_host;
my @servers;			   # servers in request order
my $chunk_begin = 1;

# for accounting only:
my %dns_requests;                  # dns requests in the interval (by req no.)
my %dns_hosts;			   # hosts requested (all time)
my %dns_hosts_before;		   # hosts requested (all time) before interval
my %dns_hosts_interval;            # hosts requested in the interval

my %user_agent_nos;		   # number corresponding to each user agent (all time)

sub max
{
    my ($a, $b) = @_;
    return ($a < $b ? $b : $a);
}

sub min
{
    my ($a, $b) = @_;
    return ($a < $b ? $a : $b);
}

sub get_domain
{
    my ($h) = @_;
    my $i = rindex($h, ".");
    my $j = rindex($h, ".", $i - 1);
    if ($i - $j == 3) # .co.uk style domain
    { $j =  rindex($h, ".", $j - 1); }
    if ($j != -1)
    { return substr($h, $j+1); }
    return $h;
}

# truncate urls to fit term's width.
sub truncate_url
{
    my ($head, $path, $dir, $file, $tail) = @_;
    # if dir too long, truncate it ...
    if ($url_trunc_first eq "path" &&
	length("$head$dir/$file$tail") > $columns)
    {
	my $len = max($columns - length("${head}[..]/$file$tail"), 0);
	$dir = "[..]" . substr($dir, max(length($dir) - $len, 0), $len);
    }
    
    if ($url_trunc_first eq "tail" &&
	length("$head$dir/$file?") > $columns)
    { 
	my $len = max($columns - length("${head}[..]/$file?"), 0);
	$dir = "[..]" . substr($dir, max(length($dir) - $len, 0), $len);
    }
    
    if ($url_trunc_first eq "balanced" &&
	length("$head$dir/$file$tail") > $columns)
    { 
	my $avail = max($columns - length("${head}[..]/$file?"), 0);
	my $tailn = min(length($tail), $avail / 2);
	my $len = $avail - $tailn;
	if ($len < length($dir))
	{
	    $dir = "[..]" . substr($dir, length($dir) - $len, $len);
	}
    }
    
    # ... truncate end if needed
    return substr("$head$dir/$file$tail", 0, $columns);
}

sub leaked_referer
{
    my ($host, $referer) = @_;
    if ($referer =~ m|http://([^/]*)|)
    {
	my $ref_host = $1;
	return ($ref_host ne $host);
    }
    return 0;
}

# can url be grouped with others (hidden) ?
# not if grouping would hide a warning, or url is suspiciously long.
sub can_group_url
{
    my ($path, $warning) = @_;
    return (!$warning &&               
	    ($group_really_long_urls || length($path) < $really_long_thres));
}

# returns head: stuff displayed to the left of the url.
#         warning: head contains a warning (don't group then!)
sub url_header
{
    my ($referer, $method, $agent) = @_;
    my $leaked_ref = leaked_referer($host, $referer);
    my $warning = ($leaked_ref || $method eq "POST");
    
    # stuff displayed to the left of the url
    my $head = (($leaked_ref ? "ref!" : "") . 
		($method eq "POST" ? "post" : ""));
    # display user agent ?    
    my $agent_str = ($show_agent ? sprintf("[%i]", $user_agent_nos{$agent}) : "");
    die("urlgulp: shouldn't happen, unknown user agent!") if (!$user_agent_nos{$agent});
    
    $head = sprintf("%-8s %s%s", $head, $agent_str, $url_spc);
    $head = sprintf("%.*s", length($url_spc), $head);
    return ($head, $warning);
}

# Naming:
#   url   http://www.google.com/abc/def/foo.js?abcd=efgh
#   host         www.google.com
#   path                       /abc/def/foo.js?abcd=efgh
#   dir                        /abc/def
#   file                                foo.js
#   tail                                      ?abcd=efgh

sub pretty_print_url
{
    my ($host) = @_;
    # display summary for images and css
    my $images = 0, $css = 0;
    foreach my $req (@{$req_by_host{$host}})
    { 
	my ($path, $referer, $method, $agent) = split(/\|/, $req);

        # head: stuff displayed to the left of the url.
        # warning: head contains a warning (don't group then!)
	my ($head, $warning) = url_header($referer, $method, $agent);

	# parse pathname part of the url into dir, file and tail
	$path =~ m|^([^?&:]*)/([^/?&:]*)(.*)$| || die("should not happen: '$path'");
	my ($dir, $file, $tail) = ($1, $2, $3);
	my $line = "$head$path";
	
	# image and css grouping
	if ($file =~ m/\.(png|jpg|jpeg|gif|ico)$/i &&
	    can_group_url($path, $warning))
	{   $images++; next; }
	
	if ($file =~ m/\.(css)$/i &&
	    can_group_url($path, $warning))
	{   $css++; next; }

        # truncate urls to fit term's width.
	if (length("$line") > $columns)
	{ $line = truncate_url($head, $path, $dir, $file, $tail); }

	# highlight file
	my $file_color = $color{wb};
	$file_color = $color{bb} if ($file =~ m|\.js|);
	$line =~ s|^([^?&:]*)/([^/?&:]*)(.*)$|$1/$file_color$2$color{e}$3|;

	# highlight warnings in $head now that we don't interfere with formatting.
	$line =~ s/ref!/$color{rb}ref!$color{e}/;
	# $line =~ s/post /$color{yb}post $color{e}/;
	print "$line\n";
    }

    # grouped stuff summary.
    if ($images)
    { printf("${url_spc}[$color{wb}images$color{e}: $images]\n"); }
    if ($css)
    { printf("${url_spc}[$color{wb}css$color{e}: $css]\n"); }
}

# barebones display, no highlighing, no coloring, no truncating.
sub raw_print_url
{
    my ($host) = @_;
    foreach my $req (@{$req_by_host{$host}})
    {
	my ($path, $referer, $method, $agent) = split(/\|/, $req);

        # head: stuff displayed to the left of the url.
        # warning: head contains a warning
	my ($head, $warning) = url_header($referer, $method, $agent);
	
	print "$head$path\n";
    }
}

###############################################################################
# dns related display

sub dns_alias_for_other_domain
{
    my ($host) = @_;
    my $domain = get_domain($host);
    foreach my $a (@{$dns_alias{$host}})
    {
	if (get_domain($a) ne $domain)
	{ return 1; }
    }
    return 0;
}

sub get_dns_aliases
{
    my ($host) = @_;
    my $dns_aliases = "";
    if (dns_alias_for_other_domain($host) ||
	$print_urls == 2)
    {
	$dns_aliases = join(", ", @{$dns_alias{$host}});
	$dns_aliases = " = $dns_aliases" if ($dns_aliases ne "");
	$dns_aliases = sprintf("%*s", $columns - 42, $dns_aliases);
	if (dns_alias_for_other_domain($host))
	{ $dns_aliases = "$color{rb}$dns_aliases$color{e}"; }
    }
    return $dns_aliases;
}

# Summary of dns requests. (to log everything use -d)
sub print_dns_reqs
{
    my $nreq = keys(%dns_requests);
    return if ($nreq == 0);
    my $str = "$color{bb}DNS:$nreq$color{e}";
    my $extra = "";

    # duplicates
    my $dups = $nreq - keys(%dns_hosts_interval);
    if ($dups)
    { $extra .= "$color{rb}$dups dup$color{e}"; }

    # already requested hosts
    my $new_reqs = keys(%dns_hosts) - keys(%dns_hosts_before);
    my $n = keys(%dns_hosts_interval) - $new_reqs;
    if ($n)
    { $extra .= ($extra eq "" ? "" : " ") . 
	  "$color{pb}$n rpt$color{e}"; 
    }
    
    $extra = ($extra eq "" ? "" : "($extra)");
    print "$str $extra\n";
}

###############################################################################
# user agent stuff

sub print_user_agents
{
    my $n = keys(%user_agent_nos);
    return if (!$n);
    printf("user agents:\n");
    for (my $i = 1; $i <= $n; $i++)
    {
	foreach my $agent (keys(%user_agent_nos))
	{
	    if ($user_agent_nos{$agent} == $i)
	    {
		printf("  [%i] %s\n", $i, $agent);
	    }
	}
    }
    print "\n";
}

sub add_user_agent
{
    my ($agent) = @_;
    return if ($user_agent_nos{$agent});
    my $n = keys(%user_agent_nos);
    $user_agent_nos{$agent} = $n + 1;
}

sub print_legend
{
    my $a = "referrer user       domain\n";
    my $b = "post     agent        url            count";
    $b = sprintf("%s%*s", $b, $columns - length($b), "dns aliases\n");
    printf("%s\n$a$b%s\n", "-" x $columns, "-" x $columns);
}


sub print_reqs
{
    my $current_domain = get_domain($servers[0]);

    print_user_agents() if ($show_agent);
    print_legend() if ($show_legend);
    print_dns_reqs();

    foreach my $host (@servers)
    {
	my $n = @{$req_by_host{$host}};
	my $port = "";
	if ($host =~ m|(.*):(.*)|)
	{
	    $host = $1;
	    $port = ":$2";
	}

	# print host. 3rd party domain -> red color
	my ($color_in, $color_out) = ($color{yb}, $color{e});
	if (get_domain($host) ne $current_domain)
	{ ($color_in, $color_out) = ($color{rb}, $color{e}); }	

	my $s = sprintf("%30s%-6s [$n]%s", $host, $port, get_dns_aliases($host));
	printf("%s%s%s\n", $color_in, $s, $color_out);

	if ($print_urls == 1)
	{ pretty_print_url($host); }

	if ($print_urls == 2)
	{ raw_print_url($host); }
    }
    print "\n";
    key_mode_prompt() if ($key_mode);
    
    %req_by_host = ();
    @servers = ();
    $chunk_begin = 1;
    %dns_requests = ();
    %dns_hosts_interval = ();
    %dns_hosts_before = %dns_hosts;
}
$SIG{ALRM} = sub { print_reqs(); };

sub add_request
{
    my ($host, $path, $referer, $method, $agent) = @_;
    if (!$req_by_host{$host})
    { push(@servers, $host); }
    push(@{$req_by_host{$host}}, "$path|$referer|$method|$agent");
    add_user_agent($agent);

    if ($key_mode)
    { 
	print ".";
	return; 
    }

    if ($wait_quiet)
    { 
	alarm $interval;
	return;
    }

    # default mode: fixed intervals
    if ($chunk_begin)
    {
	$chunk_begin = 0;
	alarm $interval;
    }
}

##############################################################
# input multiplexing and parsing

# what a hack ... =)
# we could use IO::BufferedSelect one day (or gnu parallel ?)
if (!$tcp_only)
{ open(IN, "$key_cmd    $tcpdump_cmd 2>&1 &    exec $urlsnarf_cmd 2>&1 | "); }
else
{ open(IN, "$key_cmd    $tcpdump_cmd 2>&1 | "); }

while (my $s = <IN>)
{
    if ($s =~ m|^key press$|)
    {
	#print "key press detected !\n";
	print "\n";
	print_reqs();
	next;
    }

    ##############################################################
    # http dump

    # 192.168.1.102 - - [11/Mar/2012:15:24:15 +0100] "GET http://portail.free.fr/im_zapette/mini_17390.jpg HTTP/1.1" - - "http://portail.free.fr/" "Opera/9.80 (X11; Linux i686; U; en) Presto/2.9.168 Version/11.50"
    # 192.168.1.102 - - [11/Mar/2012:15:21:52 +0100] "POST http://www.javascript-coder.com/files/htm-form-tutorial/html-form-tutorial-example-1.html HTTP/1.1" - - "http://www.javascript-coder.com/files/htm-form-tutorial/html-form-tutorial-example-1.html" "Opera/9.80 (X11; Linux i686; U; en) Presto/2.9.168 Version/11.50"
    if ($s =~ m|"(GET) http://([^/]*)(/[^ ]*) HTTP[^"]*" - - "([^"]*)" "([^"]*)"|   ||
	$s =~ m|"(POST) http://([^/]*)(/[^ ]*) HTTP[^"]*" - - "([^"]*)" "([^"]*)"|)
    {
	my ($method, $host, $path, $referer, $agent) = ($1, $2, $3, $4, $5);
	#printf("%s|%s|%s\n", $host, $path, $referer, $method);
	add_request($host, $path, $referer, $method, $agent);
	next;
    }

    ##############################################################
    # non http dump

    if ($s =~ m/\[\|([a-z]*)\]/)    
    {
	my $proto = $1;
	print "WARNING: tcpdump: truncated packet ($proto), increase -s\n";
    }


    # dns:
    # 13:23:11.498652 IP 192.168.1.102.37199 > 192.168.1.1.53: 28106+ A? imp.free.fr. (29)
    # 13:23:11.527717 IP 192.168.1.1.53 > 192.168.1.102.37199: 28106 1/0/0 A 212.27.48.1 (45)

    # 14:07:06.541093 IP 192.168.1.102.45903 > 192.168.1.1.53: 36366+ A? yahoo.com. (27)
    # 14:07:06.542041 IP 192.168.1.1.53 > 192.168.1.102.45903: 36366 3/0/0 A 98.139.183.24, A 98.139.127.62, A 209.191.122.70 (75)
    # 14:07:06.863547 IP 192.168.1.102.48173 > 192.168.1.1.53: 38339+ A? www.yahoo.com. (31)
    # 14:07:06.892851 IP 192.168.1.1.53 > 192.168.1.102.48173: 38339 4/0/0 CNAME fp3.wg1.b.yahoo.com., CNAME eu-fp3-lfb.wa1.b.yahoo.com., CNAME eu-fp3.wa1.b.yahoo.com., A 87.248.112.181 (121)

    # Example of DNS Alias to another Domain:
    # 19:50:26.437890 IP 192.168.1.102.44541 > 192.168.1.1.53: 44119+ A? stats.wired.com. (33)
    # 19:50:26.467811 IP 192.168.1.1.53 > 192.168.1.102.44541: 44119 20/3/3 CNAME condenast.112.2o7.net., A 66.235.138.18, ...

    # dns request
    if ($s =~ m|> [0-9.]*\.53: ([0-9]*)[+]* A\? (.*)\.|)    
    {
	my ($reqn, $host) = ($1, $2);
	$dns_req{$reqn} = $host;
	# accounting
	$dns_requests{$reqn} = 1;
	$dns_hosts{$host} = 1;
	$dns_hosts_interval{$host} = 1;
	if ($debug >= 2) { print "DNS request [$reqn] $host\n"; }
	next;
    }

    # successful dns reply
    if ($s =~ m|[0-9.]*\.53 > .*: ([0-9]*)[-*]* [0-9]+/[0-9]+/[0-9]+( .*)|)
    {
	my ($reqn, $ans) = ($1, $2);
	my $host = $dns_req{$reqn};
	if ($host eq "")
	{ 
	    print "WARNING: dns reply with no matching dns request...\n"; 
	    next;
	}
	if ($debug) { print "DNS reply   [$reqn] $host: "; }

	my @tab = split(/,/, $ans);
	@{$dns_alias{$host}} = ();
	foreach my $a (@tab)
	{
	    if ($a =~ m| A ([0-9.]*)|)
	    {
		my $ip = $1;
		$rev_dns{$ip} = $host;
		if ($debug) { print "$ip "; }
		next;
	    }

	    # DNS Alias
	    if ($a =~ m| CNAME (.*)\.|)
	    {
		my $name = $1;
		if ($debug >= 2) { print "=$name "; }
		# filter out provider stuff
		if (!$filtered_cdns{get_domain($name)})
		{ 
		    if ($debug == 1) { print "=$name "; }
		    push(@{$dns_alias{$host}}, $name); 
		}
	    }
	}
	if ($debug) { print "\n"; }
	next;
    }

    # ipv6 dns request
    if ($s =~ m|> [0-9.]*\.53: ([0-9]*)\+ AAAA\? (.*)\.|)    
    {
	my $host = $2;
	print "WARNING: ipv6 DNS request for $host. I'm not hanlding ipv6!\n";
	next;
    }

    # tcp syn (ex: https):
    # 12:48:01.297490 IP bilbo-netbook.43708 > router.https: Flags [S], seq 2842818660, win 5840, options [mss 1460,sackOK,TS val 111719333 ecr 0], length 0

    if ($s =~ m|> ([0-9.]*)\.([^.]*):.*Flags|)
    {
	my ($ip, $port) = ($1, $2);
	my $host = $ip;
	$host = $rev_dns{$ip} if ($rev_dns{$ip} ne "");
	# printf("%s:%s|???\n", $host, $port);
	add_request("$host:$port", "???", "-", "-");

	next;
    }

    # messages we can safely ignore (FIXME: show in debug mode ?)
    if ($s =~ m|^tcpdump: verbose output suppressed|  ||
	$s =~ m|^listening on .*, capture size 500 bytes|  ||
	$s =~ m|^urlsnarf: listening on|)
    { next; }

    ##############################################################
    
    print $s;
}

