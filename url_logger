#!/usr/bin/perl

my $print_urls = 0;

#my $url_trunc_first = "tail";
#my $url_trunc_first = "path";
my $url_trunc_first = "balanced";

%color = ( "wb" => "\033[40;37;1m",  # white bold
	   "rb" => "\033[40;31;1m",  # red bold
	   "yb" => "\033[40;33;1m",  # yellow bold
	   "pb" => "\033[40;35;1m",  # purple bold
	   "bb" => "\033[40;34;1m",  # blue bold
	   "cb" => "\033[40;36;1m",  # cyan bold
	   "e" => "\033[0m");
my $color_seq_len = 14;

my $url_spc = "                    ";

if ($ARGV[0] eq "-v")
{
    $print_urls = 1;
    $columns = `tput cols`;
    shift(@ARGV);
}

if ($ARGV[0] eq "-vv")
{
    $print_urls = 2;
    shift(@ARGV);
}

# Turn off buffering on stdout
$|=1;

##############################################################
# http dump

my $cmd1 = "urlsnarf -n";
#my $cmd1 = "cat testurltrunc.log";

##############################################################
# non http dump
# TODO: log udp traffic
# TODO: log ipv6 traffic

my $pcap_filter = "udp port 53 or (not tcp port 80 and tcp[tcpflags] = tcp-syn)";
my $cmd2 ="tcpdump -s 500 -l -n -i eth0 '$pcap_filter'"; 

my %dns_req;
my %rev_dns;

my %dns_alias;
my %filtered_cdns = (
    "akamai.net" => 1,
    "akadns.net" => 1, 
    "akamaiedge.net" => 1,
    "bluehatnetwork.com" => 1,
    "cachefly.net" => 1,
    "cdngc.net" => 1,
    "cubecdn.net" => 1,
    "cedexis.net" => 1,
    "cloudfront.net" => 1,
    "cloudflare.com" => 1,
    "cotcdn.net" => 1,
    "edgekey.net" => 1,
    "edgecastcdn.net" => 1,
    "edgesuite.net" => 1,
    "fastly.net" => 1,
    "footprint.net" => 1,
    "hwcdn.net" => 1,
    "instacontent.net" => 1,
    "internapcdn.net" => 1,
    "llnwd.net" => 1,
    "lxdns.com" => 1,
    "mirror-image.net" => 1,
    "netdna-cdn.com" => 1,
    "panthercdn.com" => 1,
    "simplecdn.net" => 1,
    "swiftcdn1.com" => 1,
    "upcache.com" => 1,
    "voxcdn.net" => 1,
    "yahoodns.net" => 1);
    
    
##############################################################
# summary display

my $interval = 5;

my %req_by_srv;
my @servers; # servers in request order
my $chunk_begin = 1;

sub max
{
    my ($a, $b) = @_;
    return ($a < $b ? $b : $a);
}

sub min
{
    my ($a, $b) = @_;
    return ($a < $b ? $a : $b);
}

sub get_domain
{
    my ($h) = @_;
    my $i = rindex($h, ".");
    my $j = rindex($h, ".", $i - 1);
    if ($i - $j == 3) # .co.uk style domain
    { $j =  rindex($h, ".", $j - 1); }
    if ($j != -1)
    { return substr($h, $j+1); }
    return $h;
}

sub pretty_print_url
{
    my ($srv) = @_;
    # display summary for images
    my $images = 0, $css = 0;
    foreach my $url (@{$req_by_srv{$srv}})
    { 
	if ($url =~ m/\.(png|jpg|jpeg|gif|ico)$/i)
	{   $images++; next; }

	if ($url =~ m/\.(css)$/i)
	{   $css++; next; }

	$url =~ m|^([^?&:]*)/([^/?&:]*)(.*)$| || die("should not happen: '$url'");
	my ($path, $file, $tail) = ($1, $2, $3);
	my $line = "$url_spc$url";

	# truncate urls to term's width
	if (length("$line") > $columns)
	{
	    # if path too long, truncate it ...
	    if ($url_trunc_first eq "path" &&
		length("$url_spc$path/$file$tail") > $columns)
	    { 
		my $len = max($columns - length("${url_spc}[..]/$file$tail"), 0);
		$path = "[..]" . substr($path, max(length($path) - $len, 0), $len);
	    }				   
	    
	    if ($url_trunc_first eq "tail" &&
		length("$url_spc$path/$file?") > $columns)
	    { 
		my $len = max($columns - length("${url_spc}[..]/$file?"), 0);
		$path = "[..]" . substr($path, max(length($path) - $len, 0), $len);
	    }

	    if ($url_trunc_first eq "balanced" &&
		length("$url_spc$path/$file$tail") > $columns)
	    { 
		my $avail = max($columns - length("${url_spc}[..]/$file?"), 0);
		my $tailn = min(length($tail), $avail / 2);
		my $len = $avail - $tailn;
		if ($len < length($path))
		{
		    $path = "[..]" . substr($path, length($path) - $len, $len);
		}
	    }

	    # ... truncate end if needed
	    $line = substr("$url_spc$path/$file$tail", 0, $columns);
	}
	# highlight file
	my $file_color = $color{wb};
	$file_color = $color{bb} if ($file =~ m|\.js|);
	$line =~ s|^([^?&:]*)/([^/?&:]*)(.*)$|$1/$file_color$2$color{e}$3|;
	print "$line\n";
    }
    if ($images)
    { printf("${url_spc}[$color{wb}images$color{e}: $images]\n"); }
    if ($css)
    { printf("${url_spc}[$color{wb}css$color{e}: $css]\n"); }
}

sub dns_alias_for_other_domain
{
    my ($host) = @_;
    my $domain = get_domain($host);
    foreach my $a (@{$dns_alias{$host}})
    {
	if (get_domain($a) ne $domain)
	{ return 1; }
    }
    return 0;
}

sub get_dns_aliases
{
    my ($srv) = @_;
    my $dns_aliases = "";
    if (dns_alias_for_other_domain($srv) ||
	$print_urls == 2)
    {
	$dns_aliases = join(", ", @{$dns_alias{$srv}});
	$dns_aliases = " = $dns_aliases" if ($dns_aliases ne "");
	$dns_aliases = sprintf("%*s", $columns - 42, $dns_aliases);
	if (dns_alias_for_other_domain($srv))
	{ $dns_aliases = "$color{rb}$dns_aliases$color{e}"; }
    }
    return $dns_aliases;
}

sub print_reqs
{
    my $current_domain = get_domain($servers[0]);

    foreach my $srv (@servers)
    {
	my $n = @{$req_by_srv{$srv}};
	my $port = "";
	if ($srv =~ m|(.*):(.*)|)
	{
	    $srv = $1;
	    $port = ":$2";
	}

	# evil stuff -> red color
	my ($color_in, $color_out) = ($color{yb}, $color{e});
	if (get_domain($srv) ne $current_domain)
	{ ($color_in, $color_out) = ($color{rb}, $color{e}); }	

	my $s = sprintf("%30s%-6s [$n]%s", $srv, $port, get_dns_aliases($srv));
	printf("%s%s%s\n", $color_in, $s, $color_out);

	if ($print_urls == 1)
	{ pretty_print_url($srv); }

	if ($print_urls == 2)
	{
	    foreach my $url (@{$req_by_srv{$srv}})
	    { printf("$url_spc%s\n", $url); }
	}
    }
    print "\n";

    %req_by_srv = ();
    @servers = ();
    $chunk_begin = 1;
}
$SIG{ALRM} = sub { print_reqs(); };

sub add_request
{
    my ($srv, $url) = @_;
    if (!$req_by_srv{$srv})
    { push(@servers, $srv); }
    push(@{$req_by_srv{$srv}}, $url);
    
    if ($chunk_begin)
    {
	$chunk_begin = 0;
	alarm $interval;
    }
}

##############################################################

# what a hack ... =)
open(IN, " $cmd2 & exec $cmd1 | "); 

while (my $s = <IN>)
{
    ##############################################################
    # http dump

    # 192.168.1.102 - - [11/Mar/2012:15:24:15 +0100] "GET http://portail.free.fr/im_zapette/mini_17390.jpg HTTP/1.1" - - "http://portail.free.fr/" "Opera/9.80 (X11; Linux i686; U; en) Presto/2.9.168 Version/11.50"
    # 192.168.1.102 - - [11/Mar/2012:15:21:52 +0100] "POST http://www.javascript-coder.com/files/htm-form-tutorial/html-form-tutorial-example-1.html HTTP/1.1" - - "http://www.javascript-coder.com/files/htm-form-tutorial/html-form-tutorial-example-1.html" "Opera/9.80 (X11; Linux i686; U; en) Presto/2.9.168 Version/11.50"
    if ($s =~ m|"GET http://([^/]*)(/[^ ]*) HTTP| ||
	$s =~ m|"POST http://([^/]*)(/[^ ]*) HTTP|)
    {
	my ($srv, $url) = ($1, $2);
	#printf("%s|%s\n", $srv, $url);
	add_request($srv,$url);
	next;
    }

    ##############################################################
    # non http dump

    if ($s =~ m/\[\|([a-z]*)\]/)    
    {
	my $proto = $1;
	print "WARNING: tcpdump: truncated packet ($proto), increase -s\n";
    }


    # dns:
    # 13:23:11.498652 IP 192.168.1.102.37199 > 192.168.1.1.53: 28106+ A? imp.free.fr. (29)
    # 13:23:11.527717 IP 192.168.1.1.53 > 192.168.1.102.37199: 28106 1/0/0 A 212.27.48.1 (45)

    # 14:07:06.541093 IP 192.168.1.102.45903 > 192.168.1.1.53: 36366+ A? yahoo.com. (27)
    # 14:07:06.542041 IP 192.168.1.1.53 > 192.168.1.102.45903: 36366 3/0/0 A 98.139.183.24, A 98.139.127.62, A 209.191.122.70 (75)
    # 14:07:06.863547 IP 192.168.1.102.48173 > 192.168.1.1.53: 38339+ A? www.yahoo.com. (31)
    # 14:07:06.892851 IP 192.168.1.1.53 > 192.168.1.102.48173: 38339 4/0/0 CNAME fp3.wg1.b.yahoo.com., CNAME eu-fp3-lfb.wa1.b.yahoo.com., CNAME eu-fp3.wa1.b.yahoo.com., A 87.248.112.181 (121)

    # Example of DNS Alias to another Domain:
    # 19:50:26.437890 IP 192.168.1.102.44541 > 192.168.1.1.53: 44119+ A? stats.wired.com. (33)
    # 19:50:26.467811 IP 192.168.1.1.53 > 192.168.1.102.44541: 44119 20/3/3 CNAME condenast.112.2o7.net., A 66.235.138.18, ...

    # dns request
    if ($s =~ m|> [0-9.]*\.53: ([0-9]*)\+ A\? (.*)\.|)    
    {
	my ($reqn, $host) = ($1, $2);
	$dns_req{$reqn} = $host;
	next;
    }

    # successful dns reply
    if ($s =~ m|[0-9.]*\.53 > .*: ([0-9]*) [0-9]+/[0-9]+/[0-9]+( .*)|)
    {
	my ($reqn, $ans) = ($1, $2);
	my $host = $dns_req{$reqn};
	my @tab = split(/,/, $ans);
	@{$dns_alias{$host}} = ();
	foreach my $a (@tab)
	{
	    if ($a =~ m| A ([0-9.]*)|)
	    {
		my $ip = $1;
		$rev_dns{$ip} = $host;
		next;
	    }

	    # DNS Alias
	    if ($a =~ m| CNAME (.*)\.|)
	    {
		my $name = $1;
		# filter out provider stuff
		if (!$filtered_cdns{get_domain($name)})
		{ push(@{$dns_alias{$host}}, $name); }
	    }
	}
	next;
    }

    # ipv6 dns request
    if ($s =~ m|> [0-9.]*\.53: ([0-9]*)\+ AAAA\? (.*)\.|)    
    {
	my $host = $2;
	print "WARNING: ipv6 DNS request for $host. I'm not hanlding ipv6!\n";
	next;
    }

    # tcp syn (ex: https):
    # 12:48:01.297490 IP bilbo-netbook.43708 > router.https: Flags [S], seq 2842818660, win 5840, options [mss 1460,sackOK,TS val 111719333 ecr 0], length 0

    if ($s =~ m|> ([0-9.]*)\.([^.]*):.*Flags|)
    {
	my ($ip, $port) = ($1, $2);
	my $host = $ip;
	$host = $rev_dns{$ip} if ($rev_dns{$ip} ne "");
	# printf("%s:%s|???\n", $host, $port);
	add_request("$host:$port", "???");

	next;
    }

    ##############################################################
    
    print $s;
}

